module frege.repl.FregeRepl where

import frege.Version
import Java.Net
import Data.List
import Interpreter.FregeInterpreter
import Compiler.Data (Symbol, Global, StG, Definition)
import Compiler.BaseTypes (Position)
import Compiler.Utilities as U()
import Compiler.EclipseUtil as EU()
import Java.Swing (JFrame, invokeLater)
import Java.Awt (Component, ActionListener)
import Java.Util (Scanner)
import Repl.Gui
import frege.Prelude hiding (print)
import frege.Prelude (print pPrint)
import Control.monad.State public(State)

data ReplEnv = ReplEnv {
  lastJavaGenSrc :: Maybe String,
  lastExternalScript :: Maybe String,
  prompt :: String,
  config :: InterpreterConfig,
  state :: InterpreterState
} where
  default = ReplEnv {
    lastJavaGenSrc = Nothing, -- To display last generated Java Source
    lastExternalScript = Nothing, -- To reload last external script
    prompt = "frege> ",
    config = InterpreterConfig.default,
    state = InterpreterState InterpreterClassLoader.default
  }

data Repl a = Repl {un :: State ReplEnv a} where
  get = Repl $ State.get
  put s = Repl $ State.put s
  modify f = do
      s <- Repl.get
      Repl.put (f s)
  run (Repl s) env = s.run env
  runWithDefault (Repl s) = s.run ReplEnv.default

instance Monad Repl where
  return = Repl . return
  (Repl state) >>= f = Repl $ state >>= (\a -> Repl.un $ f a)


data ReplResult = ReplInfo [Message]
                | CodeInfo {
                    sourceRepr :: SourceInfo,
                    compilerState :: Global,
                    messages :: [Message]
                  }
                | JavaSource String
                | DocHelp String

repl :: ConsoleReader -> ReplEnv -> IO ()
repl console env = do
  cmd <- read console env
  let (res, newEnv) = Repl.run (eval cmd) env
  print console cmd res newEnv
  loop console cmd res newEnv

read :: ConsoleReader -> ReplEnv -> IO Command
read console state = do
   console.println ""
   line <- console.readLine state.prompt
   let readAgain = read console state
       readResource path = do
         res <- slurp path
         either (\msg -> console.println (show msg) >> readAgain) return res
   case Command.parse line of
     Left err -> (console.println err) >> readAgain
     Right DefMulti -> readMultiline console >>= (return . Eval)
     Right Reload -> maybe
       (console.println "No source to reload!" >> readAgain) readResource state.lastExternalScript
     Right (Load path) -> readResource path
     Right cmd -> return cmd

print :: ConsoleReader -> Command -> ReplResult -> ReplEnv -> IO ()
print _ Java (JavaSource src) _ = javaSourceGUI src
print _ (HelpDoc src) (DocHelp doc) _ = if null doc then return () else helpGUI src doc
print console _ (ReplInfo msgs) _ = printMessages console msgs
print console (cmd@(Eval line)) (CodeInfo sourceRepr g msgs) env = case sourceRepr of
  Expression sym | isVariable g sym =
    if isString g sym
      then do
              valMaybe <- fieldValue (symbolClass sym g) (symbolVar sym) env.state.classLoader
              console.println $ maybe "undefined" id valMaybe
           `catch` (console.println . showThrowableCause)
      else do
        let evalShow = do
                        let varDecl = variableDeclScript sym.name.base line
                        when (line /= sym.name.base) $
                          Repl.modify ReplEnv.{config <- InterpreterConfig.{predefs <- ((:) varDecl)}}
                        eval (Eval $ buildShowScript sym.name.base g sym)
        let (res, newEnv) = Repl.run evalShow env
        case res of
          CodeInfo{} -> print console cmd res newEnv
          _ -> printSymbolType console g sym
  Expression sym = printSymbolType console g sym
  Definitions syms = do
    let symLabels = map (showSymbol g) $ filter (not . Symbol.{alias?}) newSyms
        newSyms = filter (\sym -> sym.pos.line > lineStart) syms
        lineStart = case env.config.predefs of
          [] -> 0
          (_:xs) -> fold (\z \x -> z + length (lines x)) 1 xs
    when (not $ null symLabels) $ console.println $ intercalate newLine symLabels
    printMessages console $ filter (\msg -> msg.pos.line > lineStart) msgs
  Module moduleName = do
    console.println $ "Module " ++ moduleName
    printMessages console msgs
print console (LoadExternal path script) res env = print console (Eval script) res env
print _ _ _ _ = return ()

loop :: ConsoleReader -> Command -> ReplResult -> ReplEnv -> IO ()
loop console Quit _ _ = return ()
loop console Reset _ _ = repl console ReplEnv.default
loop console _ _ env = repl console env

eval :: Command -> Repl ReplResult
eval (Eval line) = do
  env <- Repl.get
  let (res, newState) = Interpreter.run (interpret line) env.config env.state
  case res of
    InterpreterResult.Success{sourceRepr=sourceRepr, compilerState=g} -> do
      case sourceRepr of
        Module{} -> do
          Repl.modify ReplEnv.{lastJavaGenSrc=Just line, state = newState}
        Definitions{} -> do
          Repl.modify ReplEnv.{lastJavaGenSrc=Nothing, config <- InterpreterConfig.{predefs<-(line:)}}
        Expression{} -> do
          Repl.modify ReplEnv.{lastJavaGenSrc=Just line, state = newState}
      return $ CodeInfo sourceRepr g (Message.fromGlobal g)
    InterpreterResult.Failure messages -> return $ ReplInfo messages

eval (Type expr) = do
  env <- Repl.get
  let res = fst $ Interpreter.run (typeof expr) env.config env.state
  return $ either ReplInfo (\typ -> ReplInfo [Message.info typ]) res

eval (LoadExternal src script) = do
  res <- eval (Eval script)
  case res of
    CodeInfo{} -> Repl.modify ReplEnv.{lastExternalScript=Just src} >> return res
    _ -> return res

eval (BrowseModule moduleName) = do
  env <- Repl.get
  let res = fst $ Interpreter.run (browseModule moduleName) env.config env.state
  return $ maybe (ReplInfo []) (\(syms, g) -> symbolsToReplInfo syms g) res

eval Browse = do
  env <- Repl.get
  let src = unlines $ reverse env.config.predefs
      res = fst $ Interpreter.run (browse src) env.config.{predefs=[]} env.state
  return $ maybe (ReplInfo []) (\(syms, g) -> symbolsToReplInfo syms g) res

eval (HelpDoc name) = do
  env <- Repl.get
  let res = fst $ Interpreter.run (docHelp name) env.config env.state
  return $ either ReplInfo DocHelp res

eval Java = do
  env <- Repl.get
  let javagenDefs [] = return (ReplInfo [Message.info "Empty source!"])
      javagenDefs defs = do
        oldEnv <- Repl.get
        Repl.modify ReplEnv.{config <- InterpreterConfig.{predefs=[]}}
        res <- java (unlines $ reverse defs)
        Repl.put oldEnv
        return res
  maybe (javagenDefs env.config.predefs) (\src -> java src) env.lastJavaGenSrc


eval History = do
  env <- Repl.get
  return $ ReplInfo . map Message.info . reverse $ env.config.predefs

eval Reset = Repl.put ReplEnv.default >> return (ReplInfo [])
eval Version = return $ ReplInfo [Message.info version]
eval Help = return $ ReplInfo [Message.info (help cmdHelp)]
eval _ = return $ ReplInfo []

symbolsToReplInfo syms g = ReplInfo $ map (Message.info . showSymbol g) wantedSyms where
  wantedSyms = filter wanted syms
  wanted (sym :: Symbol)
    | sym.{alias?} = false
    | otherwise = true

java src = do
    env <- Repl.get
    let res = fst $ Interpreter.run (javaSource src) env.config env.state
    return $ either ReplInfo JavaSource res

slurp :: String -> IO (Message | Command)
slurp (urlSpec@#.*?://.*#) = do
      url <- URL.new urlSpec
      script <- urlContents url
      return . Right $ LoadExternal urlSpec script
  `catch` handleIOE where
    handleIOE (ioe :: IOException) = return $ Left (Message.error (showThrowableCause ioe))
slurp filePath = do
   script <- fileContents filePath
   return . Right . LoadExternal filePath $ script
 `catch` handleIOE where
    handleIOE (ioe :: IOException) = return $ Left (Message.error (showThrowableCause ioe))

printMessages :: ConsoleReader -> [Message] -> IO ()
printMessages console [] = return ()
printMessages console msgs = console.println $ Message.showMessages msgs

printSymbolType (console :: ConsoleReader) g sym = do
                        console.print (CharSequence.fromString ":: ")
                        console.println (getSymbolType g sym)

readMultiline :: ConsoleReader -> IO String
readMultiline console = intercalate newLine . reverse <$> f [] where
  f acc = do
    line <- console.readLine ""
    if (line == ":}")
      then console.println "" >> return acc
      else f (line : acc)


cmdHelp = [(":t or :type <expression>", "Display the type of an expression."),
            (":browse <module name>", "Display the names in a module if " ++
                "a module name is provided otherwise display the names in the default REPL module."),
            (":load <url or file>", "Load Frege code snippets from an URL or file."),
            (":java", "View Java translation of last compiled Frege source."),
            (":r", "Reload the last url or file source."),
            (":history", "Display the source history for definitions in the default REPL module."),
            (":reset", "Reset the session discarding all evaluated scripts."),
            (":version", "Display Frege version."),
            (":{", "Start multiline definitions."),
            (":}", "End multiline defintions."),
            (":help <name>", "Display the documentation for the given name." ++
                            " If the name is not provided, display this help message."),
            (":q or :quit", "Quit REPL")]

renderCmdHelp cmdHelp = map render cmdHelp where
    maxLeftIndent = length . fst $ maximumBy (compare `on` (length . fst)) cmdHelp
    indent cmd desc width = cmd ++ (concat $ replicate (width - cmd.length) " ") ++
        " - " ++ desc
    render (cmd, desc) = indent cmd desc maxLeftIndent

help cmdHelp = intercalate newLine $ header ++ body where
    header = ["At the prompt, you can enter Frege code snippets to get them evaluated.",
            "The output or compilation errors will be printed below the prompt.",
            "In addition to Frege code, the following commands are supported:\n"]
    body = renderCmdHelp cmdHelp

data ConsoleReader = mutable native jline.console.ConsoleReader where
  native new :: () -> IO ConsoleReader throws IOException
  native readLine :: ConsoleReader -> String -> IO String throws IOException
  native readLine' readLine :: ConsoleReader -> IO String throws IOException
  native println :: ConsoleReader -> String -> IO () throws IOException
  native print :: ConsoleReader -> CharSequence -> IO () throws IOException
  native setPrompt :: ConsoleReader -> String -> IO ()
  native setExpandEvents :: ConsoleReader -> Bool -> IO ()

urlContents url = do
    inStream <- URL.openStream url
    scanner <- scannerFromInputStream inStream
    scanner.useDelimiter #\Z#
    scanner.next `finally` scanner.close

fileContents filePath = do
    file <- File.new filePath
    scanner <- Scanner.new file "utf-8"
    scanner.useDelimiter #\Z#
    scanner.next `finally` scanner.close

native scannerFromInputStream new :: InputStream -> IOMutable Scanner

javaSourceGUI :: String -> IO ()
javaSourceGUI javaSource = do
   frame::JFrame <- JFrame.new "Java Source"
   newContentPane::JPanel <- BorderLayout.new () >>= JPanel.new
   frame.setDefaultCloseOperation JFrame.dispose_on_close
   editor::JEditorPane <- JEditorPane.new "text/plain" javaSource
   editor.setEditable false
   scrollPane <- JScrollPane.new editor
   dimension <- Dimension.new 600 600
   JComponent.setPreferredSize scrollPane dimension
   Container.add newContentPane scrollPane (asObject BorderLayout.center)
   frame.setContentPane newContentPane
   frame.pack
   frame.setVisible true


helpGUI :: String -> String -> IO ()
helpGUI title content = do
    frame <- JFrame.new (title ++ " - Documentation")
    frame.setDefaultCloseOperation JFrame.dispose_on_close
    newContentPane:: JPanel <- BorderLayout.new () >>= JPanel.new
    let html :: String
        html
            | index >= 0 = substring content index  -- Skip DOCTYPE
            | otherwise = content
          where
            index = indexOf content "<HTML>"
    editor::JEditorPane <- JEditorPane.new "text/html" html
    editor.setEditable false
    helpLinksListener <- FregeJavaProxy.with showDesktop HyperlinkListener.clazz
    editor.addHyperlinkListener helpLinksListener
    scrollPane <- JScrollPane.new editor
    dimension <- Dimension.new 600 600
    JComponent.setPreferredSize scrollPane dimension
    Container.add newContentPane scrollPane (asObject BorderLayout.center)
    newContentPane.setOpaque true
    frame.setContentPane newContentPane
    frame.pack
    frame.setVisible true

data FregeJavaProxy = pure native frege.interpreter.javasupport.FregeJavaProxy where
  native with frege.interpreter.javasupport.FregeJavaProxy.with ::
    (Object -> Method -> ObjectArray -> ST s a) -> Class c -> IO c

showDesktop :: Object -> Method -> ObjectArray -> IO ()
showDesktop _ _ args = do
  event <- return $ asHyperlinkEvent $ args.elemAt 0
  d <- Desktop.getDesktop ()
  desktopSupported <- d.isDesktopSupported
  let url = HyperlinkEvent.getURL event
      navigateRelative = either throw d.browse . URI.new $ fixHelpLink event.getDescription
      navigate = maybe navigateRelative (\u -> toURI u >>= d.browse) url
  when (event.getEventType == HyperlinkEvent_EventType.activated) navigate


derive ArrayElement Object
type ObjectArray = JArray Object

fixHelpLink s | Just (Just relative : _)
                  <- s `match` #^\.\.(.*)$# = "http://www.frege-lang.org/doc/frege" ++ relative
fixHelpLink s = s

native toURI :: URL -> ST s URI throws URISyntaxException
pure native asObject "(java.lang.Object)" :: a -> Object


pure native isEmpty :: String -> Bool
pure native replaceAll :: String -> String -> String -> String
pure native indexOf :: String -> String -> Int
pure native substring :: String -> Int -> String
                         | String -> Int -> Int -> String

data Command = Version | Help | HelpDoc String | Browse |
    BrowseModule String | Type String | Load String | LoadExternal String String | Eval String |
    Java | DefMulti | History | Reload | Reset | Skip | Quit where

    parse   :: String -> Either String Command
    parse s = parse' $ trim s where
        parse' "" = Right Skip
        parse' #^:# = parseCommand s
        parse' script = Right $ Eval script

    parseCommand s | Just (Just moduleName : _) <- s `match` #:browse\s+(.*)# = Right $ BrowseModule moduleName
    parseCommand s | Just (Just path : _) <- s `match` #^:l.*?\s+(.*)# = Right $ Load path
    parseCommand s | Just (Just src : _) <- s `match` #:help\s+(.*)# = Right $ HelpDoc src
    parseCommand s | Just (Just expr : _) <- s `match` #^:t.*?\s+(.*)# = Right $ Type expr
    parseCommand  ":version" = Right Version
    parseCommand ":browse" = Right $ Browse
    parseCommand ":{" = Right DefMulti
    parseCommand ":history" = Right History
    parseCommand ":java" = Right Java
    parseCommand ":r" = Right Reload
    parseCommand ":reset" = Right Reset
    parseCommand ":q" = Right Quit
    parseCommand ":quit" = Right Quit
    parseCommand ":help" = Right Help
    parseCommand otherwise = Left "Invalid Command"

derive Eq Command
derive Show Command

vmVendor = maybe "" id $ System.getProperty "java.vm.vendor"
vmName = maybe "" id $ System.getProperty "java.vm.name"
javaVersion = maybe "" id $ System.getProperty "java.version"

pure native toString :: Object -> String
pure native trim :: String -> String

welcome = "Welcome to Frege " ++ version ++ " (" ++ vmVendor ++ " " ++ vmName ++
    ", " ++ javaVersion ++ ")"

main _ = do
  console <- ConsoleReader.new ()
  console.setPrompt ReplEnv.default.prompt
  console.setExpandEvents false
  console.println welcome
  repl console ReplEnv.default
  System.exit 0
